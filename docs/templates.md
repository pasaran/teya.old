## Шаблоны


### Примеры

Определение шаблонов:

```
button
    BUTTON.btn
        ...

icon
    IMG.icon
        @src = ...

link ( href = "#" )
    A.link
        @href = href
        ...
```

Вызов шаблонов:

```
    button "Ok"

    button
        icon "ok.png"
        "Ok"

    link
        href: "https://mail.yandex.ru"
        "Яндекс.Почта"
```


### HTML с "дыркой"

Большая часть шаблонов (компонентов) представляет собой кусок html'я с "дыркой":

```html
<button type="button">
    <!-- content -->
</button>
```

Иногда, конечно, "дырок" может быть несколько:

```html
<div class="page">
    <div class="left">
        <!-- content -->
    </div>
    <div class="right">
        <!-- content -->
    </div>
</div>
```

Для упрощения написания шаблонов с одной "дыркой"
(а их подавляющее большинство), предусмотрен специальный параметр.


### Параметры

#### Параметр `...`

Каждый шаблон имеет предопределенный параметр `...`, имеющий [тип](types.md) `xml`.

```
    button "Ok"

    button
        icon "ok.png"
        "Ok"
```

В первом случае, значением этого параметра будет строка `"Ok"`,
а во втором — результат вызова шаблона `icon` плюс строка `"Ok"`
(т.е. все, что расположено в блоке под именем шаблона и с оступом).

В `...` можно передать и html-атрибуты:

```
    button
        @id = "ok-button"
        "Ok"
```

#### Именованные параметры

Помимо параметра `...`, шаблоны могут использовать и обычные параметры:

```
link ( href = "#" )
    A.link
        @href = href
        ...

page ( xml left, xml right )
    DIV.page
        DIV.left left
        DIV.right right

```

В первом случае у шаблона один [скалярный](types.md) параметр (`scalar` — это дефолтный
тип для параметров). И у этого параметра задано дефолтное значение — `"#"`.

```
    link href: "/" "Home"

    //  Тоже самое, но в несколько строк.
    link
        href: "/"
        "Home"
```

Вызов без параметра:

```
    link
        @onclick = "do_cancel()"
        "Cancel"
```

Во втором случае у шаблона два параметра. Им задан [тип](types.md) `xml`.
При этом при вызове шаблона мы можем указывать параметры в любом порядке.
Или даже пропускать параметры:

```
    page
        right: "Right"
        left: "Left"

    page
        right: "Right"
```


### Контекст

Шаблон всегда выполняется в каком-то контексте. В этом контексте вычисляются
относительные [jpath'ы](jpath.md):

```
item
    LI.link
        link
            href: .href
            .title
```


### Смена контекста

По-дефолту, при вызове шаблона (или функции) контекст не меняется.
Но его можно явно указать, если необходимо:

```
    item -> .item

    item -> {
        href: "/first/"
        title: "First"
    }
```

